dependencies {
  swaggerUI 'org.webjars:swagger-ui:4.1.3'
}

openapi3 {
  server = "https://api.suwiki.kr"
  title = 'SUWIKI API'
  description = 'SUWIKI API 문서입니다.'
  version = "${applicationVersion}"
  outputFileNamePrefix = "suwiki-api-spec"
}

var swaggerSource = toDir("api-spec/${openapi3.outputFileNamePrefix}.${openapi3.format}").asFile
var copyInto = toDir("resources/main/static/docs")

swaggerSources {
  suwiki {
    setInputFile(swaggerSource)
  }
}

tasks.withType(project.GenerateSwaggerUI).configureEach {
  dependsOn "${project.path}:openapi3"
}

tasks.register("copySwaggerUiIntoJar", Copy) {
  group 'documentation'
  description 'copy Swagger UI which generated by task into jar'

  dependsOn "${project.path}:generateSwaggerUISuwiki"
  from "${generateSwaggerUISuwiki.outputDir}"
  into copyInto

  tasks.resolveMainClassName.mustRunAfter "${project.path}:copySwaggerUiIntoJar"
}

bootJar {
  dependsOn "${project.path}:copySwaggerUiIntoJar"
}

def toDir(String path) {
  return layout.buildDirectory.dir(path).get()
}
